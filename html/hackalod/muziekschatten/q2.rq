PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX som: <http://data.muziekschatten.nl/som/>

select distinct ?eventStart  ?eventLabel ?eventMedia ?eventMediaCaption
(concat(GROUP_CONCAT(distinct ?omschrijving; separator="; " ),
        "; <br> Bron: <a href=", ?url, ">Muziekschatten</a>") as ?eventDescription)
{
  # selecteer composities
  ?s a schema:MusicComposition .

  # zet de titel in het label van op de tijdlijn
  ?s schema:name ?eventLabel .

  # haal het them veld op hierop gaan we straks filteren
  ?s som:SXTHEM ?thema .

  # haal de url naar de muziekschatten pagina op
  ?s schema:url ?url .

  # bepaal de publicatie datum uit één van de twee datum velden
  ?s schema:dateIssued|schema:datePublished ?date .

  # in het keywords veld staat uitgebreiden informatie dit voegen samen
  # tot de beschrijving van het evenement via de group_concat in de select hierboven
  ?s schema:keywords ?omschrijving .

  # maak een schoon jaar uit de datum velden
  BIND( IF(datatype(?date)=xsd:date,(str(year(?date))),(replace(str(?date),"[ c o p . ? \\[ \\] ]",""))) as ?datestr)

  # filter de resultaten op het thema songfestival
  filter(contains(lcase(?thema),"songfestival"))

  # probeer nu aan de hand van het performer veld een afbeelding
  # op te halen uit de nl.dbpedia data
  OPTIONAL {

    # de performer is een zelfstandig resource met eigenschappen
    ?s schema:performer ?performer .

    # sla de naam van de artiest op
    ?performer schema:name ?naam .

    # haal de externe links op naar dezelfde definitie in andere datasets
    ?performer owl:sameAs ?external_link .

    # gebruik alleen de nl.dbpedia link
    filter(contains(lcase(str(?external_link)),"nl.dbpedia.org"))

    # roep nu de nl.dbpedia service aan
    service <http://nl.dbpedia.org/sparql>
    {
      # en probeer een link naar een afbeelding te vinden
      # we gebruiken hier de DBpedia link voor en niet de Wikidata omdat
      # die meerdere plaatjes kan hebben en daarmee worden de resultaten verdubbeld
      ?external_link dbo:thumbnail ?image
    }
    # als een afbeelding gevonden is zet die in op de tijdlijn
    BIND(if(bound(?image),?image,'') as ?eventMedia )

    # en zet eventueel de naam onder de afbeelding
    BIND(if(bound(?eventMedia),?naam,'') as ?eventMediaCaption)
  }
  # zet de geschoonde datum in de lijst voor de tijd
  BIND(?datestr as ?eventStart)

  # groepeer de resultaten zodat de distinct boven goed werkt.
}
group by ?eventStart ?eventLabel ?eventMedia ?eventMediaCaption ?url

# en zet dit in de volgorde van tijd
order by ?eventStart
