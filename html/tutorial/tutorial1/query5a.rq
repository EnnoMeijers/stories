# definities als verkorte schrijfwijze
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <http://schema.org/>
prefix thes: <http://data.bibliotheken.nl/id/thes/>

# We passen hier alleen de projetie aan, maar de functie count doet op de achtergrond natuurlijk wel een aggregatie
select (count(?boek) as ?aantal)  {

    # selecteer alles met type boek
    ?boek a schema:Book .

    # haal de titel van het boek op
    ?boek schema:name ?titel .

    # selecteer alles over 'dieren'
    {
        ?boek schema:about thes:p075635178
    }
    # of
    UNION
    # alles met 'dieren' als bovenliggende term
    {
        ?boek schema:about ?onderwerp_uri .
        ?onderwerp_uri skos:broader+ thes:p075635178 .
    }

    #Haal de auteur op bij een boek
    ?boek schema:author ?nta_uri .

    #En de auteur naam
    ?nta_uri schema:name ?auteur .

    # en filter alles uit de jaren 60
    # haal het jaar op uit het publicatie 'event'
    # hoewel hier een schema:startDate is gebruikt is het alleen met een jaartal gevuld
    ?boek schema:publication/schema:startDate ?jaar .

    # zet het jaar om naar een getal (integer) en filter dan het bereik tussen 1960 en 1970
    FILTER(xsd:integer(?jaar)>1959 && xsd:integer(?jaar)<1970)

    # tel alleen de boeken met gethesaureerde auteurs (daar kunnen we misschien meer over te weten komen)
    # even nagerekend: hiermee vallen twee boeken uit deze set
    FILTER(!isblank(?nta_uri))
}
